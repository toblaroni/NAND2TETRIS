// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with increment, load, and reset modes.
 * if      (inc(t))   out(t+1) = out(t) + 1
 * else if (load(t))  out(t+1) = in(t)
 * else if (reset(t)) out(t+1) = 0
 * else               out(t+1) = out(t)
 *
 * To select a mode, assert the relevant control bit,
 * and de-assert the other two bits. 
 */
CHIP PC {
    IN in[16], inc, load, reset;
    OUT out[16];
    
    PARTS:
    // --- inc control bit ---
    Inc16 (in=registerout, out=regoutplus1);
    Mux16 (a=registerout, b=regoutplus1, sel=inc, out=afterincbit);

    // --- load control bit ---
    Mux16 (a=afterincbit, b=in, sel=load, out=afterloadbit);

    // --- reset control bit ---
    Mux16 (a=afterloadbit, b=false, sel=reset ,out=registerIn);

    // --- register load bit --- 
    Or (a=inc, b=load, out=incOrload);
    Or (a=reset, b=incOrload, out=loadregister);
    Register (in=registerIn, load=loadregister, out=registerout, out=out);

}